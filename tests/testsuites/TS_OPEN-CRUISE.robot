*** Settings ***
Documentation       Suite Open cruise

Resource            %{PROJECT_FOLDER}/tests/BaseTests.resource
Resource            %{PROJECT_FOLDER}/tests/step-defs/StepConnexion.resource
Resource            %{PROJECT_FOLDER}/tests/step-defs/StepSouscription.resource
Resource            %{PROJECT_FOLDER}/tests/step-defs/StepGestionCompte.resource

Suite Setup         BaseTests.Suite Setup
Suite Teardown      BaseTests.Suite Teardown
Test Setup          BaseTests.Test Setup
Test Teardown       BaseTests.Test Teardown

Test Tags           type_tnr    env_${env}    browser_${browser}
# type_
# env_
# browser_
# jira_
# test_
# dev_


*** Test Cases ***
Se Connecter Compte Admin
    [Documentation]    Se Connecter Compte Admin
    [Tags]    jira_01    test_positive    dev_done
    StepConnexion.Choisir Langue    en
    StepConnexion.Se Connecter    ${COMPTE_ADMIN}
    StepConnexion.Verifier Connexion Ok    ${COMPTE_ADMIN}

Se Connecter Compte Invalide
    [Documentation]    Se Connecter Compte Invalide
    [Tags]    jira_02    test_negative    dev_done
    &{compte_invalide}    Create Dictionary    username=XXXXXXXX    password=YYYYYYYY
    StepConnexion.Se Connecter    ${compte_invalide}
    StepConnexion.Verifier Connexion Ko    CNX_ERROR_00001

Souscrire Compte Part
    [Documentation]    Souscrire Compte Part
    [Tags]    jira_03    test_positive    dev_done    type_smoke_test
    ${nouveau_compte}    Charger Compte    data-part.json
    ${nouveau_compte}    StepSouscription.Generation Mail Aleatoire    ${nouveau_compte}
    StepSouscription.Saisir Compte Partie Commune    ${nouveau_compte}
    StepSouscription.Saisir Compte Partie Part    ${nouveau_compte}
    StepSouscription.Valider Souscription
    StepConnexion.Se Connecter    ${nouveau_compte}
    StepConnexion.Verifier Connexion Ko    CNX_ERROR_00003
    StepConnexion.Se Connecter    ${COMPTE_ADMIN}
    StepConnexion.Verifier Connexion Ok    ${COMPTE_ADMIN}
    StepGestionCompte.Activer Compte    ${nouveau_compte}
    StepConnexion.Se Deconnecter
    StepConnexion.Se Connecter    ${nouveau_compte}
    StepConnexion.Verifier Connexion Ok    ${nouveau_compte}

Souscrire Compte Pro
    [Documentation]    Souscrire Compte Pro
    [Tags]    jira_04    test_positive    dev_done    type_smoke_test
    ${nouveau_compte}    Charger Compte    data-pro.json
    ${nouveau_compte}    StepSouscription.Generation Mail Aleatoire    ${nouveau_compte}
    StepSouscription.Saisir Compte Partie Commune    ${nouveau_compte}
    StepSouscription.Saisir Compte Partie Pro    ${nouveau_compte}
    StepSouscription.Valider Souscription
    StepConnexion.Se Connecter    ${nouveau_compte}
    StepConnexion.Verifier Connexion Ko    CNX_ERROR_00003
    StepConnexion.Se Connecter    ${COMPTE_ADMIN}
    StepConnexion.Verifier Connexion Ok    ${COMPTE_ADMIN}
    StepGestionCompte.Activer Compte    ${nouveau_compte}
    StepConnexion.Se Deconnecter
    StepConnexion.Se Connecter    ${nouveau_compte}
    StepConnexion.Verifier Connexion Ok    ${nouveau_compte}

Souscription Compte Existant
    [Documentation]    Souscription Compte Existant
    [Tags]    jira_05    test_negative    dev_done
    ${compte_existant}    Charger Compte    data-pro.json
    # Utilisation du mail admin existant
    Set To Dictionary    ${compte_existant}    username=${COMPTE_ADMIN}[username]
    StepSouscription.Saisir Compte Partie Commune    ${compte_existant}
    StepSouscription.Saisir Compte Partie Pro    ${compte_existant}
    StepSouscription.Verifier Souscription Ko    CNX_ERROR_00006

Souscription En Erreur Champs Obligatoires FR
    [Documentation]    Souscription En Erreur Champs Obligatoires
    [Tags]    jira_06    test_negative    dev_done
    StepSouscription.Valider Messages Champs Obligatoires

Souscription En Erreur Champs Obligatoires EN
    [Documentation]    Souscription En Erreur Champs Obligatoires
    [Tags]    jira_06    test_negative    dev_done
    StepConnexion.Choisir Langue    en
    StepSouscription.Valider Messages Champs Obligatoires

mauvaisepratiquedecrituredutest
    Log To Console    message
    # à utiliser dès le commencement du projet afin de traiter les warning au plus tôt
    # ceci pour montrer les warnings de robocop (analyse statique)
    # robotidy à lancer régulièrement pour formater le code, cela réduit aussi les warnings
    # les comentaires avec le mot TODO sont à préconiser pour se rappeler d'avoir un point à améliorer
    # lancer la tache robocop que se trouve dans .vscode\tasks.json
    # CRTL SHIFT P
    # >Tasks: Run Task
    # RobotFramework : robotidy
    # puis
    # RobotFramework : robocop

    # Report generated by Robocop version: 4.1.1
    # Issues by ID:
    # W0701 (todo-in-comment)                 : 2
    # I0923 (unnecessary-string-conversion)   : 1
    # W0202 (missing-doc-test-case)           : 1
    # W0308 (not-capitalized-test-case-title) : 1
